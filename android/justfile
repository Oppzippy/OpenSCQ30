build profile='dev':
    #!/usr/bin/env bash
    set -euo pipefail

    case "{{profile}}" in
        dev)
            ./gradlew assembleDebug
            ;;
        release)
            ./gradlew assembleRelease
            ;;
        *)
            echo Invalid profile
            exit 1
    esac

test:
    cargo test
    ./gradlew testDebugUnitTest

test-cov:
    cargo llvm-cov --no-report

ktlint-paths := "build.gradle.kts app/build.gradle.kts app/src/**/*.kt"
alias fmt := format

[parallel]
format: format-rust format-kotlin
[private]
format-rust:
    -cargo fmt
[private]
format-kotlin:
    -ktlint --editorconfig=../.editorconfig -F {{ ktlint-paths }}


[parallel]
format-check: format-check-rust format-check-kotlin
[private]
format-check-rust:
    cargo fmt --check
[private]
format-check-kotlin:
    ktlint --editorconfig=../.editorconfig {{ ktlint-paths }}

