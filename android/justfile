build profile='dev':
    #!/usr/bin/env bash
    set -euo pipefail

    case "{{profile}}" in
        dev)
            ./gradlew assembleBluetoothDebug
            ;;
        release)
            ./gradlew assembleBluetoothRelease
            ;;
        *)
            echo Invalid profile
            exit 1
    esac

test:
    cargo test
    ./gradlew testDebugUnitTest

test-cov:
    cargo llvm-cov --no-report

ktlint-paths := "build.gradle.kts app/build.gradle.kts app/src/**/*.kt"
alias fmt := format

[parallel]
format: format-rust format-kotlin
format-rust:
    -cargo fmt
format-kotlin:
    -ktlint --editorconfig=../.editorconfig -F {{ ktlint-paths }}


[parallel]
format-check: format-check-rust format-check-kotlin
format-check-rust:
    cargo fmt --check
format-check-kotlin:
    ktlint --editorconfig=../.editorconfig {{ ktlint-paths }}

